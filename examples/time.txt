# The grammar below describes English expressions specifying a span of time or a particular moment.
# It covers expressions for past and present moments but offers much thinner coverage for future
# moments.
#
# This is almost precisely the grammar used by App::JobLog.
# see http://search.cpan.org/search?query=App%3A%3AJobLog&mode=module

                   <ROOT> = <ever> | <span>

                   <span> = <date> [ <span_divider> <date> ]?
                   <date> = <numeric> | <verbal>
                 <verbal> = <my> | <named_period> | <relative_period> | <month_day> | <full>
           <named_period> = <modifiable_day> | <modifiable_month> | <modifiable_period>
                   <full> = <at_time_on> <full_no_time> | <full_no_time> <at_time>
         <modifiable_day> = <at_time_on> <modifiable_day_no_time> | <modifiable_day_no_time> <at_time>
              <month_day> = <at_time_on> <month_day_no_time> | <month_day_no_time> <at_time>
                <numeric> = <year> | <ym> | <at_time_on> <numeric_no_time> | <numeric_no_time> <at_time>
        <relative_period> = [ <at> <s>? ]? <time> <s> <relative_period_no_time> | <relative_period_no_time> <at_time> | <now>
                <at_time> = [ [ <s> | <s>? <at> <s>? ]? <time> ]?
             <at_time_on> = [ <at> <s> ]? <time> <s> <on> <s>
           <full_no_time> = <dm_full> | <md_full>
       <modifiable_month> = [ <month_modifier> <s> ]? <month>
      <modifiable_period> = [ <period_modifier> <s> ]? <period>
      <month_day_no_time> = <month_first> | <day_first>
        <numeric_no_time> = <us> | <iso> | <md> | <dom>
              <day_first> = <dom> <s> <month>
                <dm_full> = <dom> <s> <month> ","? <s> <year>
                    <iso> = <year> <divider> <dom> 2 <dom>
                     <md> = <dom> <divider> <dom>
                <md_full> = <month> <s> <dom> "," <s> <year>
 <modifiable_day_no_time> = [ <modifier> <s> ]? <weekday>
            <month_first> = <month> <s> <dom>
         <month_modifier> = <modifier> | <termini> [ <s> <of> ]?
                     <my> = <month> ","? <s> <year>
        <period_modifier> = <modifier> | <termini> [ <s> <of> [ <s> <the> ]? ]?
                   <time> = <num_time> [ <s>? <time_suffix> ]?
                     <us> = <dom> <divider> <dom> 2 <year>
                     <ym> = <year> <divider> <dom>
                <divider> = "-" | "/" | "."
                  <month> = <full_month> | <short_month>
               <num_time> = <dom> [ ":" <second> [ ":" <second> ]? ]?
                 <period> = <non_pay> | <pay>
           <span_divider> = <s>? "-"++ | <through> <s>?
                <termini> = [ <the> <s> ]? <beginning> | <end>
                <weekday> = <full_weekday> | <short_weekday>
                     <at> = / at | @ /ix
              <beginning> = / beg ( in ( ning )? )? /ix
                    <dom> = /\b\d{1,2}\b/
                    <end> = / end /ix
                   <ever> = / all | always | ever /ix
             <full_month> = / january | february | march | april | may | june | july | august | september | october | november | december /ix
           <full_weekday> = / sunday | monday | tuesday | wednesday | thursday | friday | saturday /ix
                   <hour> = /\b\d{1,2}\b/
                 <minute> = /\b\d{2}\b/
               <modifier> = / last | this /ix
                <non_pay> = / week | month | year /ix
                    <now> = / now /ix
                     <of> = / of /ix
                     <on> = / on /ix
                    <pay> = / pay (?: \s*+ period )? | pp /ix
<relative_period_no_time> = / yesterday | today /ix
                      <s> = /\s++/
                 <second> = /\b\d{2}\b/
            <short_month> = / jan | feb | mar | apr | may | jun | jul | aug | sep | oct | nov | dec /ix
          <short_weekday> = / sun | mon | tue | wed | thu | fri | sat /ix
                    <the> = / the /ix
                <through> = / through | thru | to | till? | until /ix
            <time_suffix> = / [ap] ( m | \.m\. ) /ix
                   <year> = /\b\d{4}\b/