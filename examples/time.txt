# The grammar below describes English expressions specifying a span of time or a particular moment.
# It covers expressions for past and present moments but offers much thinner coverage for future
# moments.
#
# This is almost precisely the grammar used by App::JobLog.
# see http://search.cpan.org/search?query=App%3A%3AJobLog&mode=module

                    <ROOT> = (s)? [ (ever) | <span> ] (s)?
                    (ever) =(?ix: all | always | ever )
                    <span> = <date> [ <span_divider> <date> ]?
 
                      (at) =(?ix: at | @ )
                 <at_time> = [ [ (s) | (s)? (at) (s)? ]? <time> ]?
              <at_time_on> = [ (at) (s) ]? <time> (s) (on) (s)
                      (on) =(?i:on)
               (beginning) =(?ix: beg ( in ( ning )? )? )
                    <date> = <numeric> | <verbal>
               <day_first> = (d){1,2} (s) <month>
                 <divider> = "-" | "/" | "."
                 <dm_full> = (d){1,2} (s) <month> ","? (s) (d){4}
                     <dom> = (d){1,2}
                    <full> = <at_time_on> <full_no_time> | <full_no_time> <at_time>
              (full_month) =(?ix: january | february | march | april | may | june | july | august | september | october | november | december )
            <full_no_time> = <dm_full> | <md_full>
            (full_weekday) =(?ix: sunday | monday | tuesday | wednesday | thursday | friday | saturday )
                     <iso> = (d){4} <divider> (d){1,2} 2 (d){1,2}
                      <md> = (d){1,2} <divider> (d){1,2}
                 <md_full> = <month> (s) (d){1,2} "," (s) (d){4}
          <modifiable_day> = <at_time_on> <modifiable_day_no_time> | <modifiable_day_no_time> <at_time>
  <modifiable_day_no_time> = [ (modifier) (s) ]? <weekday>
        <modifiable_month> = [ <month_modifier> (s) ]? <month>
       <modifiable_period> = [ <period_modifier> (s) ]? <period>
                (modifier) =(?ix: last | this ) 
                   <month> = (full_month) | (short_month) 
               <month_day> = <at_time_on> <month_day_no_time> | <month_day_no_time> <at_time>
       <month_day_no_time> = <month_first> | <day_first>
             <month_first> = <month> (s) (d){1,2}
          <month_modifier> = (modifier) | <termini> [ (s) (of) ]?
                      <my> = <month> ","? (s) <year>
            <named_period> = <modifiable_day> | <modifiable_month> | <modifiable_period> 
                     (now) =(?ix: now )
                 <numeric> = <year> | <ym> |<at_time_on> <numeric_no_time> | <numeric_no_time> <at_time>
         <numeric_no_time> = <us> | <iso> | <md> | <dom>
                     (pay) =(?ix: pay (?: \s*+ period )? | pp )
                  <period> = (non_pay) | (pay)
                 (non_pay) =(?ix: week | month | year )
         <period_modifier> = (modifier) | <termini> [ (s) (of) [ (s) (the) ]? ]?
                      (of) =(?i:of)
                     (the) =(?i:the)
         <relative_period> = [ (at) (s)? ]? <time> (s) (relative_period_no_time) | (relative_period_no_time) <at_time> | (now)
 (relative_period_no_time) =(?ix: yesterday | today )
             (short_month) =(?ix: jan | feb | mar | apr | may | jun | jul | aug | sep | oct | nov | dec )
           (short_weekday) =(?ix: sun | mon | tue | wed | thu | fri | sat ) 
            <span_divider> = (s)? [ "-"++ | (through) ] (s)?
                 (through) =(?ix: through | thru | to | till? | until )
                 <termini> = [ (the) (s) ]? [ (beginning) | (end) ]
                     (end) =(?i:end)
                    <time> = (d){1,2} [ ":" (d){2} [ ":" (d){2} ]? ]? [ (s)? (time_suffix) ]?
             (time_suffix) =(?ix: [ap] ( m | \.m\. ) )
                      <us> = (d){1,2} <divider> (d){1,2} 2 (d){4}
                  <verbal> = <my> | <named_period> | <relative_period> | <month_day> | <full>  
                 <weekday> = (full_weekday) | (short_weekday)
                    <year> = (d){4}
                      <ym> = <year> <divider> (d){1,2}
                       (d) =\d
                       (s) =\s++